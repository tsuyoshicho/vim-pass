" ___vital___
" NOTE: lines between '" ___vital___' is generated by :Vitalize.
" Do not mofidify the code nor insert new lines before '" ___vital___'
function! s:_SID() abort
  return matchstr(expand('<sfile>'), '<SNR>\zs\d\+\ze__SID$')
endfunction
execute join(['function! vital#_vimpass#Hash#OTP#import() abort', printf("return map({'_vital_depends': '', 'hotp': '', 'totp': '', 'default': '', '_vital_loaded': ''}, \"vital#_vimpass#function('<SNR>%s_' . v:key)\")", s:_SID()), 'endfunction'], "\n")
delfunction s:_SID
" ___vital___
" Utilities for HOTP/TOTP
" tp
" RFC 4226 - HOTP: An HMAC-Based One-Time Password Algorithm https://tools.ietf.org/html/rfc4226
" RFC 6238 - TOTP: Time-Based One-Time Password Algorithm https://tools.ietf.org/html/rfc6238

let s:save_cpo = &cpo
set cpo&vim

let s:DEFAULT = {
      \ 'HOTP' : {
      \   'digit'   : 6,
      \   'algo'    : 'SHA1',
      \   'counter' : 8,
      \ },
      \ 'TOTP' : {
      \   'digit'   : 6,
      \   'algo'    : 'SHA1',
      \   'period'  : 30,
      \ },
      \}

function! s:_vital_loaded(V) abort
  let s:V = a:V
  let s:bitwise = s:V.import('Bitwise')
  let s:HMAC = g:V.import('Hash.HMAC')
  let s:List = g:V.import('Data.List')
endfunction

function! s:_vital_depends() abort
  return ['Bitwise',
        \ 'Hash.HMAC',
        \ 'Data.List']
endfunction

function! s:hotp(key, counter, algo, digit) abort
  let hmac = s:HMAC.new(a:algo, a:key)
  if s:DEFAULT.HOTP.counter == len(a:counter)
    let counter = a:counter
  elseif s:DEFAULT.HOTP.counter > len(a:counter)
    let counter = s:List.new(s:DEFAULT.HOTP.counter,0)
    for i in range(s:DEFAULT.HOTP.counter)
      if 0 <= (i - (s:DEFAULT.HOTP.counter - len(a:counter)))
        let counter[i] = a:counter[i - (s:DEFAULT.HOTP.counter - len(a:counter))]
      endif
    endfor
  else
    call s:_throw(printf('counter size over:%d', len(a:counter)))
  endif

  let hmac_list =  hmac.calc(counter)

  let offset = s:bitwise.and(hmac_list[-1],0xf)
  let bincode = s:bitwise.and(s:_bytes2int32_be(hmac_list[offset:offset+3]), 0x7FFFFFFF)

  let modulo_base = float2nr(pow(10, a:digit))
  let hotp_value = bincode % modulo_base

  return printf('%0' . string(a:digit) . 'd', hotp_value)
endfunction

function! s:totp(key, period, algo, digit) abort
  let now_sec = localtime()
  let epoch_sec = 0

  let counter = s:_int322bytes_be(float2nr(floor((now_sec - epoch_sec) / a:period)))
  return s:hotp(a:key, counter, a:algo, a:digit)
endfunction

function! s:default() abort
  return deepcopy(s:DEFAULT)
endfunction

"---------------------------------------------------------------------
" misc

function! s:_uint8(n) abort
  return s:bitwise.and(a:n, 0xFF)
endfunction

function! s:_uint32(n) abort
  return s:bitwise.and(a:n, 0xFFFFFFFF)
endfunction

function! s:_str2bytes(str) abort
  return map(range(len(a:str)), 'char2nr(a:str[v:val])')
endfunction

function! s:_bytes2str(bytes) abort
  return join(map(a:bytes, 'printf(''%02x'', v:val)'), '')
endfunction

function! s:_bytes2int32_be(bytes) abort
  return  s:bitwise.or(s:bitwise.lshift(a:bytes[0], 24), 
        \ s:bitwise.or(s:bitwise.lshift(a:bytes[1], 16),
        \ s:bitwise.or(s:bitwise.lshift(a:bytes[2], 8),
        \ a:bytes[3])))
endfunction

function! s:_int322bytes_be(value) abort
  return [s:_uint8(s:bitwise.rshift(a:value, 24)),
        \ s:_uint8(s:bitwise.rshift(a:value, 16)),
        \ s:_uint8(s:bitwise.rshift(a:value, 8)),
        \ s:_uint8(a:value)]
endfunction

function! s:_throw(message) abort
  throw 'vital: Hash.OTP: ' . a:message
endfunction

let &cpo = s:save_cpo
unlet s:save_cpo

" vim:set et ts=2 sts=2 sw=2 tw=0:
